plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

author = project.findProject("author")
version = project.findProperty(project.name + "version")
group = 'com.github.mcengine'

allprojects {
    group = 'com.github.mcengine'

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://oss.sonatype.org/content/repositories/central'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
    }
}

subprojects {
    apply plugin: 'java'

    // Set submodules info
    version = project.findProperty(project.name + "version")

    // Disable the 'jar' task
    jar.enabled = false

    // Bukkit server
    if (project.name == "spigot") {
        // replace
        processResources {
            def props = [author: author, version: version]
            inputs.properties props
            filteringCharset 'UTF-8'
            filesMatching('plugin.yml') {
                expand props
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/MCEngine/artifact')
            credentials {
                username = System.getenv('GITHUB_USER')
                password = System.getenv('MCENGINE_GITHUB_TOKEN_PKG')
            }
        }

        maven {
            name = 'mavenCentral'
            url = uri('https://oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username = System.getenv('SONATYPE_USER')
                password = System.getenv('SONATYPE_TOKEN')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact shadowJar
        }
    }
}

dependencies {
    implementation project(path: ':common', configuration: 'shadow')
    implementation project(path: ':spigot', configuration: 'shadow')
}

shadowJar {
    version = version
    archiveClassifier = null
    archiveBaseName.set('MCEngineUpdater')
}

jar.enabled = false
build.dependsOn(shadowJar)
